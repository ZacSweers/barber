ext {
  androidPlugin = 'com.android.tools.build:gradle:2.1.0'
  minSdkVersion = 14
  targetSdkVersion = 23
  compileSdkVersion = 23
  buildToolsVersion = '23.0.3'

  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7

  gradleVersionsPlugin = 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
  aptPlugin = 'com.neenbedankt.gradle.plugins:android-apt:1.8'

  ci = 'true'.equals(System.getenv('CI'))

  def supportVersion = '23.4.0'
  supportAnnotations = "com.android.support:support-annotations:$supportVersion"
  supportV4 = "com.android.support:support-v4:$supportVersion"
  supportAppCompat = "com.android.support:appcompat-v7:$supportVersion"

  arraySetBackport = 'io.sweers.arraysetbackport:arraysetbackport:1.0.0'
  javapoet = 'com.squareup:javapoet:1.7.0'
  autoService = 'com.google.auto.service:auto-service:1.0-rc2'

  junit = 'junit:junit:4.12'
  robolectric = 'org.robolectric:robolectric:3.0'
}

subprojects { project ->
  buildscript {
    repositories {
      jcenter()
    }
    dependencies {
      classpath rootProject.ext.androidPlugin
      classpath rootProject.ext.aptPlugin
      classpath rootProject.ext.gradleVersionsPlugin
    }
  }

  repositories {
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  if (project.plugins.hasPlugin("java")) {
    project.sourceCompatibility = JavaVersion.VERSION_1_7
    project.targetCompatibility = JavaVersion.VERSION_1_7
  }
}

task genReadMe << {
  def template = file('README.md.template').text
  def result = template.replaceAll("%%version%%", VERSION_NAME)
  file("README.md").withWriter { it << result }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
